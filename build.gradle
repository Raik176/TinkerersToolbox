plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id "com.modrinth.minotaur" version "2.+"
}

version = project.mod_version

tasks.register('buildInfo') {
    try {
        def proc = "git rev-parse --short HEAD".execute()
        proc.waitFor()
        if (proc.exitValue() == 0) {
            ext.rev = proc.text.trim()
        } else {
            throw new Exception("lolz")
        }
    } catch (ignored) {
        ext.rev = "N/A"
    }

    def build_env = System.getenv("BUILD_NUMBER")
    if (build_env != null) {
        ext.buildNum = build_env
    } else {
        ext.buildNum = "dev.${ext.rev}"
    }
}
def artifact_version = "${mod_version}.${project.buildInfo.buildNum}"
version = "${project.minecraft_version}-${artifact_version}"
println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Version: ${version}"

processResources {
    inputs.property "mod_id", project.mod_id
    inputs.property "mod_version", version
    inputs.property "loader_version", project.loader_version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "fabric_version", project.fabric_version

    filesMatching("fabric.mod.json") {
        expand "mod_id": project.mod_id,
            "mod_version": version,
            "loader_version": project.loader_version,
            "minecraft_version": project.minecraft_version,
            "fabric_version": project.fabric_version
    }
}

group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "${project.mod_id}" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

}

fabricApi {
    configureDataGeneration()
}

repositories {
    maven {
        name = "Jared's maven" //JEI
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "ModMaven" // JEI Fallback
        url = "https://modmaven.dev"
    }
    maven {
        name = "CurseMaven" // Jade
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    //Fabric API
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    //JEI //TODO: fully implement
    modCompileOnlyApi("mezz.jei:jei-${project.minecraft_version}-fabric-api:${project.jei_version}")
    //modRuntimeOnly("mezz.jei:jei-${project.minecraft_version}-fabric:${project.jei_version}")

    //Jade
    modImplementation "curse.maven:jade-324717:${project.jade_version}"
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "${mod_id}"
    versionName = "Tinkerer's Toolbox ${project.mod_version} Fabric"
    versionNumber = "${version}-fabric"
    versionType = "alpha"
    uploadFile = remapJar
    gameVersions = ["1.21.1"]
    loaders = ["fabric", "quilt"]
    dependencies {
        required.version "fabric-api", "${fabric_version}"
        optional.project "jade"
        optional.project "jei"
    }
    syncBodyFrom = rootProject.file("README.md").text
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}
